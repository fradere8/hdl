// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

 /** Процессор Hack (центральный процессор), состоящий из ALU,
 * двух регистров с именами A и D и программного счетчика PC.
 * Процессор предназначен для получения и выполнения инструкций, написанных на
 * машинном языке Hack. В частности, он функционирует следующим образом:
 * Выполняет введенную команду в соответствии со спецификацией языка Hack machine. 
 * Символы D и A в спецификации языка
 * относятся к регистрам, находящимся в центральном процессоре, в то время как M относится к внешним регистрам
 * ячейка памяти, адресованная A, т.е. к памяти[A]. Входные данные inM 
 * содержат значение этого местоположения. Если текущей команде требуется 
 * записать значение в M, значение помещается в outM, адрес 
 * значение * целевого местоположения помещается в выходные данные
 * addressmи используется управляющий бит * writeM. (Когда writeM==0, в outM может отображаться любое значение ). 
 * Выходные данные outM и writeM являются комбинационными: 
 * на них мгновенно воздействует выполнение текущей инструкции 
 * Выходы addressmи pc синхронизированы: хотя на них
 * влияет выполнение текущей команды, они принимают 
 * свои новые значения только на следующем временном шаге. Если значение reset==1, то 
 * Процессор переключается на адрес 0 (т.е. на следующем временном шаге для ПК устанавливается значение 0), 
 * а не на адрес, полученный в результате выполнения текущей команды. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Декодер инструкций
    Or16(a=false, b=instruction, 
         out[15]=isC,        // Бит 15: 1=C-инструкция, 0=A-инструкция
         out[12]=aBit,       // Бит 12: выбор второго операнда ALU (А или М)
         out[11]=c1, out[10]=c2, out[9]=c3, out[8]=c4, out[7]=c5, out[6]=c6,
         out[5]=d1, out[4]=d2, out[3]=d3,
         out[2]=j1, out[1]=j2, out[0]=j3);
    And(a=out[15], b= true, out= isC);

    // Регистр А
    // Определяем когда загружать регистр A:
    // - Для A-инструкций: всегда загружаем (isC=0)
    // - Для C-инструкций: когда d1=1 (записать результат в A)
    Not(in=isC, out=isA);              // isA = 1 для A-инструкций
    And(a=isC, b=d1, out=AluToA);      // Для C-инстр. с d1=1
    Or(a=isA, b=AluToA, out=loadA);    // Объединяем условия
    
    // Выбираем что загружать в регистр A:
    Mux16(a=instruction, b=outAlu, sel=isC, out=inputA);

    // Регистр A с двумя выходами:
    ARegister(in=inputA, load=loadA, out=ARegOut, out[0..14]=addressM); // в памяти могут храниться только 15 битные адреса (максимум 32767 слов)

    // Регистр D
    And(a=isC, b=d2, out=loadD);       // Загружать D только для C-инстр. с d2=1
    DRegister(in=outAlu, load=loadD, out=outD); // при а инструкции регистр д не меняется

    // ALU 
    // Выбираем второй операнд для ALU:
    Mux16(a=ARegOut, b=inM, sel=aBit, out=AorM);
    
    // Арифметико-логическое устройство:
    ALU(x=outD, y=AorM, 
        zx=c1, nx=c2, zy=c3, ny=c4, f=c5, no=c6, 
        out=outAlu, out=outM, zr=zr, ng=ng);

    // Условия для прыжков
    Not(in=ng, out=notNg); // >= 0
    Not(in=zr, out=notZr); // != 0
    And(a=notNg, b=notZr, out=gt);     // GT = (результат > 0)
    
    And(a=ng, b=j1, out=resLT);       // JLT: результат < 0
    And(a=zr, b=j2, out=resEQ);       // JEQ: результат = 0
    And(a=gt, b=j3, out=resGT);       // JGT: результат > 0
    
    Or(a=resLT, b=resEQ, out=temp);
    Or(a=temp, b=resGT, out=pass); 
    And(a=isC, b=pass, out=PCload);    // Прыжок только для C-инструкций
    
    // Программный счетчик
    PC(in=ARegOut, load=PCload, inc=true, reset=reset, out[0..14]=pc);

    // Записываем в память
    And(a=isC, b=d3, out=writeM);      // Запись только для C-инстр. с d3=1
}